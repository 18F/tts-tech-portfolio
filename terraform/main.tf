terraform {
  required_version = "~> 0.13.0"
  required_providers {
    github = {
      source  = "hashicorp/github"
      version = "~> 2.4"
    }
  }
}

provider "github" {
  token        = var.github_token
  organization = "18f"
}

locals {
  repos = {
    "aws-admin" : {},
    "before-you-ship" : {},
    "billing-tools" : { archived = true },
    "bug-bounty" : {},
    "certificate-service" : { archived = true },
    "chandika" : { archived = true },
    "chat" : {},
    "deploy-ttslicenses" : { archived = true },
    "dns" : { skip_issue_templates = true },
    "ghad" : {},
    "handbook" : { skip_issue_templates = true },
    "laptop" : { skip_issue_templates = true },
    "raktabija" : { archived = true },
    "slack-export-handling" : { archived = true },
    "tts-tech-portfolio-private" : {},
    "tts-tech-portfolio" : {},
    "vulnerability-disclosure-policy" : {},
  }
}

module "repo" {
  source = "./repo"

  for_each        = { for repo, config in local.repos : repo => config if ! lookup(config, "archived", false) }
  repo            = each.key
  issue_templates = lookup(each.value, "skip_issue_templates", false) ? [] : ["general.md"]
}

resource "local_file" "github_repos" {
  content = <<MARKDOWN
<!-- this file is generated by Terraform -->

# GitHub Repos

| repo | status |
| --- | --- |
%{~for repo, config in local.repos}
| [18F/${repo}](https://github.com/18F/${repo}) | ${lookup(config, "archived", false) ? "archived" : "active"} |
%{~endfor}

## Adding a repository

When a repository is considered "managed" by the Tech Portfolio, it should be included in the following places:

- [The list in Terraform](../terraform/main.tf)
- Linked repositories for project boards
  - [Team board](https://github.com/orgs/18F/projects/11/settings/linked_repositories)
  - [Pull requests](https://github.com/orgs/18F/projects/19/settings/linked_repositories)
MARKDOWN

  filename             = "${path.module}/../how_we_work/github.md"
  file_permission      = "0644"
  directory_permission = "0755"
}
